load("@bazel_skylib//rules:write_file.bzl", "write_file")
load("@pybind11_bazel//:build_defs.bzl", "pybind_extension")
load("@rules_python//python:py_binary.bzl", "py_binary")
load("@rules_python//python:py_library.bzl", "py_library")
load("@rules_symforce//symforce_tools:gen.bzl", "cc_gen_pkg", "cc_sym_util_pkg", "lcm_pkg", "py_gen_pkg")
load("@rules_symforce//symforce_tools:lcmgen.bzl", "cc_lcm_library", "py_lcm_library")

package(default_visibility = ["//visibility:public"])

cc_lcm_library(
    name = "symforce_lcm_lib",
    src = "lcmtypes/symforce.lcm",
)

cc_lcm_library(
    name = "symforce_types_lcm_lib",
    src = ":symforce_types",
)

py_lcm_library(
    name = "symforce_lcm_py",
    srcs = [
        "lcmtypes/symforce.lcm",
        "lcmtypes/symforce_types.lcm",
        "third_party/eigen_lcm/lcmtypes/eigen_lcm.lcm",
    ],
)

COPTS = [
    "-std=c++20",
]

DEFINES = ["SKYMARSHAL_PRINTING_ENABLED"]

cc_library(
    name = "skymarshal_core",
    hdrs = [
        "third_party/skymarshal/include/lcm/lcm_coretypes.h",
        "third_party/skymarshal/include/lcm/lcm_cpptypes.hpp",
        "third_party/skymarshal/include/lcm/lcm_reflection.hpp",
        "third_party/skymarshal/include/lcm/lcm_reflection_eigen.hpp",
    ],
    copts = COPTS,
    defines = DEFINES,
    includes = ["third_party/skymarshal/include"],
)

cc_library(
    name = "eigen_lcm",
    hdrs = glob([
        "third_party/eigen_lcm/lcmtypes/eigen_lcm_lcm/cpp/lcmtypes/eigen_lcm/*.hpp",
    ]),
    copts = COPTS,
    defines = DEFINES,
    includes = ["third_party/eigen_lcm/lcmtypes/eigen_lcm_lcm/cpp/"],
)

cc_library(
    name = "symforce",
    srcs = glob(
        [
            "symforce/**/*.h",
            "symforce/**/*.hpp",
            "symforce/**/*.tcc",
            "symforce/**/*.cc",
        ],
        exclude = [
            "symforce/opt/**",
            "symforce/pybind/**",
            "symforce/benchmarks/**",
            "symforce/examples/**",
            "symforce/codegen/backends/**",
            "symforce/experimental/**",
        ],
    ),
    copts = COPTS,
    defines = DEFINES,
    includes = ["."],
    linkstatic = True,
    deps = [
        ":gen_lib",
        ":opt",
        "@eigen",
        "@spdlog",
    ],
)

# Codegen templates needed for runtime code generation
filegroup(
    name = "codegen_templates",
    srcs = glob(["symforce/codegen/backends/*/templates/**"]),
)

py_library(
    name = "py",
    srcs = glob(
        [
            "symforce/**/*.py",
        ],
        exclude = [
            "symforce/codegen/backends/*/templates/**",
            # this is only used for initial import of the backend configs:
            # they are overridden in all tools with the rules_symforce ones
            "symforce/examples/**",
            "symforce/benchmarks/**",
        ],
    ),
    data = [":codegen_templates"],
    deps = [
        "@symenginepy",
        "@symforce_pypi//graphviz",
        "@symforce_pypi//jinja2",
        "@symforce_pypi//numpy",
        "@symforce_pypi//ruff",
        "@symforce_pypi//sympy",
    ],
)

# the python bindings all filled out with generated types as well
# with sym
py_library(
    name = "symforce_python",
    data = [
        ":cc_sym",
        "@rules_symforce//symforce_tools:backend_configs",
    ],
    deps = [
        ":py",
        ":symforce_lcm_py",
        ":symforce_sym",
        "@symenginepy",
    ],
)

# all the registered GEO TYPES
GEO_TYPES = [
    "sf.Rot2",
    "sf.Rot3",
    "sf.Pose2",
    "sf.Pose3",
    "sf.Unit3",
]

GROUP_GEO_TYPES = [
    "sf.Rot2",
    "sf.Rot3",
    "sf.Pose2",
    "sf.Pose3",
]

ALL_GEO_TYPES = GEO_TYPES + [
    "sf.Quaternion",
    "sf.DualQuaternion",
    "sf.Complex",
]

GEO_FACTORS_TYPES = [
    "sf.Rot2",
    "sf.Rot3",
    "sf.V3",
    "sf.Pose2",
    "sf.Pose3",
]

CAM_TYPES = [
    "symforce.cam.atan_camera_cal.ATANCameraCal",
    "symforce.cam.double_sphere_camera_cal.DoubleSphereCameraCal",
    "symforce.cam.equirectangular_camera_cal.EquirectangularCameraCal",
    "symforce.cam.linear_camera_cal.LinearCameraCal",
    "symforce.cam.orthographic_camera_cal.OrthographicCameraCal",
    "symforce.cam.polynomial_camera_cal.PolynomialCameraCal",
    "symforce.cam.spherical_camera_cal.SphericalCameraCal",
]

# run codegen generators for the gen/ directory

# this generates the symforce_sym package
# it needs access to an interpreter that doesn't have sym already
# all other gen packages use this as a dep.
# it should be setup first
py_gen_pkg(
    name = "symforce_sym",
    cam_types = CAM_TYPES,
    geo_types = GEO_TYPES,
)

# the folowing two are the cc versions
cc_gen_pkg(
    name = "geo_package",
    geo_types = GEO_TYPES,
)

cc_gen_pkg(
    name = "cam_package",
    cam_types = CAM_TYPES,
    geo_types = GEO_TYPES,
)

cc_gen_pkg(
    name = "geo_factors",
    # generate between factors for these types
    geo_types = GEO_FACTORS_TYPES,
)

cc_gen_pkg(
    name = "slam_factors",
    cam_types = CAM_TYPES,
)

cc_sym_util_pkg(
    name = "sym_util_package",
    cam_types = CAM_TYPES,
    geo_types = GEO_TYPES,
)

lcm_pkg(
    name = "symforce_types",
    cam_types = CAM_TYPES,
    geo_types = GEO_TYPES,
)

cc_library(
    name = "gen_lib",
    srcs = [
        ":cam_package",
        ":geo_factors",
        ":geo_package",
        ":slam_factors",
        ":sym_util_package",
    ],
    hdrs = [
        ":cam_package",
        ":geo_factors",
        ":geo_package",
        ":slam_factors",
        ":sym_util_package",
    ],
    copts = COPTS,
    defines = DEFINES,
    deps = [
        ":cam_package",
        ":geo_factors",
        ":geo_package",  # provides headers and includes
        ":skymarshal_core",
        ":slam_factors",
        ":sym_util_package",
        ":symforce_lcm_lib",
        ":symforce_types_lcm_lib",
        "@eigen",
    ],
)

# values needs to know about all the types that are genned
# this allows for injection of user defined types here
cc_library(
    name = "values",
    srcs = [
        "symforce/opt/key.cc",
        "symforce/opt/values.cc",
        "symforce/opt/values.tcc",
    ],
    hdrs = [
        "symforce/opt/assert.h",
        "symforce/opt/internal/hash_combine.h",
        "symforce/opt/key.h",
        "symforce/opt/values.h",
    ],
    copts = COPTS,
    defines = DEFINES,
    includes = ["."],
    deps = [
        ":gen_lib",
        "@fmt",
    ],
)

write_file(
    name = "null_tick_tock",
    out = "null_tick_tock.h",
    content = [
        "#pragma once",
        "#define SYM_TIME_SCOPE(fmt_str, ...)",
    ],
)

cc_library(
    name = "opt",
    srcs = glob(
        [
            "symforce/opt/**/*.cc",
        ],
        exclude = [
            "symforce/opt/sparse_cholesky/**",
            "symforce/opt/values.cc",
        ],
    ),
    hdrs = glob(
        [
            "symforce/opt/**/*.h",
            "symforce/opt/**/*.tcc",
        ],
        exclude = [
            "symforce/opt/sparse_cholesky/**",
            "symforce/opt/values.h",
            "symforce/opt/values.tcc",
        ],
    ),
    #    + ["null_tick_tock.h"],
    copts = COPTS,
    defines = DEFINES,
    # the following disables all stats collection
    #    defines = ["SYMFORCE_TIC_TOC_HEADER=<null_tick_tock.h>"],
    includes = ["."],
    deps = [
        ":eigen_lcm",
        ":skymarshal_core",
        ":sparse_cholesky",
        ":values",
        "@eigen",
        "@fmt",
        "@metis",
        "@spdlog",
    ],
)

cc_library(
    name = "sparse_cholesky",
    srcs = glob([
        "symforce/opt/sparse_cholesky/**/*.cc",
        "symforce/opt/sparse_cholesky/**/*.tcc",
    ]),
    hdrs = glob([
        "symforce/opt/sparse_cholesky/**/*.h",
        "symforce/opt/assert.h",
    ]),
    copts = COPTS,
    defines = DEFINES,
    deps = [
        "@eigen",
        "@fmt",
        "@metis",
        "@spdlog",
    ],
)

cc_library(
    name = "slam",
    srcs = glob([
        "symforce/slam/**/*.cc",
    ]),
    hdrs = glob([
        "symforce/slam/**/*.h",
    ]),
    copts = COPTS,
    defines = DEFINES,
    includes = ["."],
    deps = [
        ":gen_lib",
        ":opt",
        "@eigen",
        "@fmt",
        "@spdlog",
    ],
)

# Pregenerated aggregate headers that include all geo/cam types
cc_library(
    name = "pregen_all_types",
    hdrs = [
        "gen/cpp/sym/all_cam_types.h",
        "gen/cpp/sym/all_geo_types.h",
    ],
    includes = ["gen/cpp"],
    deps = [
        ":gen_lib",
    ],
)

py_library(
    name = "skymarshal",
    srcs = glob(["third_party/skymarshal/skymarshal/**/*.py"]),
    data = glob(["third_party/skymarshal/**"]),
    imports = ["third_party/skymarshal"],
    deps = [
        "@symforce_pypi//jinja2",
        "@symforce_pypi//numpy",
        "@symforce_pypi//ply",
    ],
)

py_binary(
    name = "skymarshalpy",
    srcs = ["third_party/skymarshal/skymarshal/__main__.py"],
    main = "third_party/skymarshal/skymarshal/__main__.py",
    deps = [
        ":skymarshal",
    ],
)

# cc_sym: Python bindings for SymForce C++ optimizer
# Note: headers are included in srcs since pybind_extension doesn't support hdrs
pybind_extension(
    name = "cc_sym",
    srcs = [
        "symforce/pybind/cc_factor.cc",
        "symforce/pybind/cc_factor.h",
        "symforce/pybind/cc_key.cc",
        "symforce/pybind/cc_key.h",
        "symforce/pybind/cc_linearization.cc",
        "symforce/pybind/cc_linearization.h",
        "symforce/pybind/cc_logger.cc",
        "symforce/pybind/cc_logger.h",
        "symforce/pybind/cc_optimization_stats.cc",
        "symforce/pybind/cc_optimization_stats.h",
        "symforce/pybind/cc_optimizer.cc",
        "symforce/pybind/cc_optimizer.h",
        "symforce/pybind/cc_slam.cc",
        "symforce/pybind/cc_slam.h",
        "symforce/pybind/cc_sym.cc",
        "symforce/pybind/cc_values.cc",
        "symforce/pybind/cc_values.h",
        "symforce/pybind/lcm_type_casters.h",
        "symforce/pybind/sym_type_casters.cc",
        "symforce/pybind/sym_type_casters.h",
    ],
    deps = [
        ":gen_lib",
        ":opt",
        ":pregen_all_types",
        ":slam",
        "@eigen",
        "@fmt",
        "@spdlog",
    ],
)
